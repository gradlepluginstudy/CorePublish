apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

repositories {
    maven { url 'https://dl.google.com/dl/android/maven2/' }
    mavenLocal()
    jcenter()
}
configurations {
    provided
}
idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}
sourceSets {
    main {
        groovy.srcDirs = ['src/main/groovy']
        java.srcDirs = ['src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    def androidGradlePluginVersion = "2.3.3"

    if (project.hasProperty('gradle3.0.0')) {
        androidGradlePluginVersion = "3.0.0"
    }

    provided "com.android.tools.build:gradle:${androidGradlePluginVersion}"
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    compile 'io.github.lizhangqu:core-release:1.0.0'
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath('io.github.lizhangqu:core-publish:1.2.0')
    }
    configurations.all {
        it.resolutionStrategy.cacheDynamicVersionsFor(5, 'minutes')
        it.resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    }
}

ext {
    PROJECT_POM_GROUP_ID = "io.github.lizhangqu"
    PROJECT_POM_ARTIFACT_ID = "core-publish"
    PROJECT_POM_VERSION = "${POM_VERSION}"

    RELEASE_REPOSITORY_URL = "file://${project.rootProject.file('repo/release')}"
    SNAPSHOT_REPOSITORY_URL = "file://${project.rootProject.file('repo/snapshot')}"
    RELEASE_REPOSITORY_USERNAME = ""
    RELEASE_REPOSITORY_PASSWORD = ""
    SNAPSHOT_REPOSITORY_USERNAME = ""
    SNAPSHOT_REPOSITORY_PASSWORD = ""
}
apply plugin: 'core.publish'

def template = '${name}/${version}'
if (project.hasProperty('gradle3.0.0')) {
    template = '${name}/gradle3.0.0/${version}'
}
release {
    tagTemplate = "${template}"
    versionKey = 'POM_VERSION'
}

//publishing {
//    publications {
//        maven(MavenPublication) {
//            artifact "${project.buildDir}/libs/${project.archivesBaseName}-${project.version}.jar"
//        }
//    }
//}


//remove install task compile 'com.android.tools.build:gradle:$androidGradlePluginVersion'
install {
    repositories {
        mavenInstaller {
            pom {
                whenConfigured { p ->
                    p.dependencies = p.dependencies.findAll {
                        dep -> (dep.groupId != "com.android.tools.build" && dep.artifactId != "gradle")
                    }
                }
            }
        }
    }
}
//remove uploadArchives task compile 'com.android.tools.build:gradle:$androidGradlePluginVersion'
uploadArchives {
    repositories {
        mavenDeployer {
            pom {
                whenConfigured { p ->
                    p.dependencies = p.dependencies.findAll {
                        dep -> (dep.groupId != "com.android.tools.build" && dep.artifactId != "gradle")
                    }
                }
            }
        }
    }
}

